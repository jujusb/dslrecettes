grammar cuisine.recipe.Recipe with org.eclipse.xtext.common.Terminals

generate recipe "http://www.recipe.cuisine/Recipe"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Model:
	defTechniques+=Technique* recipes+=Recipe*;
	
Technique:
	"define" name=WORD ("[" objectsFac+=IngrUslTech "]" | objects+=IngrUslTech | "{" choices "}")+ ;

fragment choices: 
	choix+=CUSTOMSTRING ("," choix+=CUSTOMSTRING)* | "{" choices "}" ("," "{" choices "}")*  
;

IngrUslTech:
	"ingredient" | "ustensil" | "preparation" | "temperature" | "tool" | 'quantity' | 'time'
;

Recipe:
	"recipe" "{" "name" ":" name=CUSTOMSTRING "time" ":" time=DOUBLE "nb_pers" ":" nb=DOUBLE ingredients=Ingredients ustensils=Ustensils instructions=Instructions "}" 
;

Ustensils:
	{Ustensils} "ustensils" ":" "{" (ust+=Ustensil (','ust+=Ustensil)*)? "}"
;

Ustensil:
	name=CUSTOMSTRING ("@" tag=WORD)? 
;

Ingredients : 
	{Ingredients} "ingredients" ":" "{" (ingr+=Ingredient (',' ingr+=Ingredient)*)? "}"
;

Ingredient:
	name=CUSTOMSTRING ":" (qte=DOUBLE (quantificateur=Quantificateurs)? | "any")("#" group=WORD)? ("@" tag=WORD)? 
;

Quantificateurs:
	{Quantificateurs} "càc" | {Quantificateurs} "cc" | {Quantificateurs} "càs" | {Quantificateurs} "cs" | {Quantificateurs} unit=Unit? ("g" | "L"| "l") 
;

Unit:
	"k" | "h" | "da" | "d" | "c" | "m" 
;

Instructions: 
	{Instructions} "instructions" ":" "{" (inst+=Instruction (';' inst+=Instruction)*)? "}"
;

Instruction:
	technique=WORD (parameters+=InstructionParameter (',' parameters+=InstructionParameter)*)?  (comment=COMMENT)? ('->' preparation=CUSTOMSTRING)?
;

Timing:
	"s" | "min" | "h" | "days"	
;
Temp: 
	"°C" | "°F"
;

InstructionParameter:
	parameter=CUSTOMSTRING | '#' tag=WORD | '@' tag=WORD | qte=DOUBLE (qt=Quantificateurs | time=Timing | temp=Temp)
;


CUSTOMSTRING:
	name+=WORD+
;

terminal DOUBLE returns ecore::EDouble : INT('.'INT)? ;
terminal WORD returns ecore::EString : '^'?('a'..'z'|'A'..'Z'|'À'..'ÿ'|'\''|'('|')') ('0'..'9'|'a'..'z'|'A'..'Z'|'À'..'ÿ'|'\''|'('|')'|'_')*;
terminal COMMENT returns ecore::EString : '"' -> '"';
